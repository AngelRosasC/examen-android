plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

apply from: '../config/versions.gradle'
apply from: '../config/variables.gradle'
apply from: '../config/functions.gradle'


android {
    compileSdk 31

    defaultConfig {
        applicationId "com.arcode.eamovies"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        applicationVariants.all {
            variant ->
                variant.buildConfigField STRING, API, getCustomProperty(API)
                variant.buildConfigField STRING, IMAGE_URL, getCustomProperty(IMAGE_URL)
                variant.buildConfigField STRING, LANGUAGE, getCustomProperty(LANGUAGE)
                variant.buildConfigField STRING, TOP_RATED, getCustomProperty(TOP_RATED)
                variant.buildConfigField STRING, POPULAR, getCustomProperty(POPULAR)
                variant.buildConfigField STRING, API_KEY, getCustomProperty(API_KEY)
                variant.buildConfigField STRING, MAP_API_KEY, getCustomProperty(MAP_API_KEY)
        }

        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
            buildConfigField BOOLEAN, DEBUG_MODE, TRUE
        }

        release {
            minifyEnabled true
            buildConfigField BOOLEAN, DEBUG_MODE, FALSE
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    // Core
    implementation(deps.androidx.core)

    // UI
    implementation(deps.androidx.appcompat)
    implementation(deps.androidx.constraintlayout)
    implementation(deps.androidx.swipe_refresh_layout)

    // Material
    implementation(deps.google.material)

    // Testing
    testImplementation(deps.test.junit)
    androidTestImplementation(deps.test.ext_junit)
    androidTestImplementation(deps.test.espresso)

    // Navigation
    implementation(deps.androidx.navigation)
    implementation(deps.androidx.navigation_ui)
    implementation(deps.androidx.activity)
    implementation(deps.androidx.fragment)

    //Lifecycle
    implementation(deps.androidx.viewmodel)
    implementation(deps.androidx.livedata)
    implementation(deps.androidx.paging)
    kapt(deps.androidx.lifecycle_compiler)

    // Hilt
    implementation(deps.google.hilt)
    kapt(deps.google.hilt_compiler)

    // Firebase
    implementation platform(deps.google.firebase)
    implementation(deps.google.firebase_analytics)
    implementation(deps.google.firebase_crashlytics)
    implementation(deps.google.firebase_firestore)

    // Google Services
    implementation(deps.google.maps)

    // Room
    implementation(deps.androidx.room)
    implementation(deps.androidx.room_ktx)
    kapt(deps.androidx.room_compiler)

    // Gson
    implementation(deps.google.gson)

    // Retrofit
    implementation(deps.square.retrofit)
    implementation(deps.square.converter_gson)

    // OkHttp
    implementation(deps.square.okhttp)
    implementation(deps.square.interceptor)

    // Glide
    implementation(deps.others.glide)
    kapt(deps.others.glide_compiler)

    // Coroutines
    implementation(deps.others.coroutines)

    // Adapter
    implementation(deps.others.base_recycler)

}

hilt {
    enableAggregatingTask = true
}